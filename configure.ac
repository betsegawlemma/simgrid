
######################
## Setup the autotools
##

AC_PREREQ(2.59)
AC_INIT([simgrid],[2.93-cvs],[simgrid2-users@listes.ens-lyon.fr])
AC_CONFIG_SRCDIR([include/gras.h])
AC_CONFIG_HEADERS([src/gras_config.h])
# A CI_PREREQ(2003.01.16) # We need a recent ACI when having sub-modules

AC_REVISION($Revision$)
AC_CANONICAL_TARGET
AC_LANG([C])

AM_INIT_AUTOMAKE(gnu)
AC_CONFIG_MACRO_DIR(acmacro) 
ACLOCAL="$ACLOCAL -I acmacro"
AC_PROG_LIBTOOL

###############
## Basic checks
##

AC_PROG_CC
AM_SANITY_CHECK
AC_PROG_MAKE_SET
AC_CHECK_PRINTF_NULL

# Checks for header files.
AC_HEADER_STDC
AC_HEADER_TIME
AC_CHECK_HEADERS([sys/socket.h \
                  sys/stat.h \
		  windows.h winsock.h winsock2.h \
		  sys/time.h \
		  errno.h unistd.h ])
AC_CHECK_FUNCS([gettimeofday usleep \
                getdtablesize \
                sysconf])

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_SIZE_T

###################################
## SimGrid and GRAS specific checks
##

# Check architecture signature begin
GRAS_ARCH
# Check architecture signature end
GRAS_CHECK_STRUCT_COMPACTION

AC_ARG_ENABLE(context,
  [  --enable-context=[ucontext/pthread]   Use either (System V) swapcontext or pthread [[default=pthread]].],,
  enable_context=pthread)
AC_MSG_CHECKING(on top of what can we build the contexts)
if test "x$enable_context" = "xucontext"; then
  AC_CHECK_HEADERS([ucontext.h])
  ac_header=ucontext.h
  as_ac_Header=`echo "ac_cv_header_$ac_header" | $as_tr_sh`
  if test `eval echo '${'$as_ac_Header'}'` = yes; then
    AC_MSG_RESULT(found ucontext.h. Great!)
  else
    ac_header=windows.h
    as_ac_Header=`echo "ac_cv_header_$ac_header" | $as_tr_sh`
    if test `eval echo '${'$as_ac_Header'}'` = yes; then
      AC_MSG_RESULT(you don't have ucontext.h but we provide an alternative for windows so you should be fine.")
    else
      AC_MSG_RESULT([[cannot find ucontext. I'm going to try as if you had set --enable-context=pthreads !]])
      enable_context=pthreads
    fi
  fi
else if test "x$enable_context" = "xpthread"; then
  AC_CHECK_HEADERS([pthread.h])
dnl A C_CHECK_LIB(pthread, pthread_mutex_lock, LIBS="$LIBS -lpthread")
  AC_CHECK_LIB(pthread,pthread_create,,
    [AC_MSG_ERROR([[Cannot find pthreads, no way (try --enable-context=ucontext if you haven't already tried).]])])
  AC_DEFINE([USE_PTHREADS],1,[Define if we USE pthreads or not])
  AC_MSG_RESULT(You have pthreads. Let's use them.)
else 
  AC_MSG_ERROR("--enable-context must be either ucontext or pthread")
fi fi

#########################################
## Check for libraries extra-dependencies
##

SIMGRID_DEP=""
AC_SUBST([SIMGRID_DEP])

GRAS_DEP=""
AC_CHECK_LIB(nsl, gethostbyname, [GRAS_DEP="$GRAS_DEP -lnsl"])
AC_CHECK_LIB(socket, connect,    [GRAS_DEP="$GRAS_DEP -lsocket"])

AC_MSG_CHECKING(for extra dependencies of libgras)
case $host_os in
  *mingw* ) GRAS_DEP="$GRAS_DEP -lws2_32" ; SIMGRID_DEP="$SIMGRID_DEP -lws2_32" ;;
esac
	   
if test "x$GRAS_DEP" = x; then 
   AC_MSG_RESULT(none)
else
   AC_MSG_RESULT($GRAS_DEP)
fi
AC_SUBST([GRAS_DEP])

##############################################
## Enhance maintainer mode and SUBST variables
## (must be placed after any compilation tests since our overprotective flags
##  let some tests fail)

AM_MAINTAINER_MODE
if test x$USE_MAINTAINER_MODE = xyes 
then
   GNOME_COMPILE_WARNINGS(yes)
fi

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

#####################
## Check for programs
##

AC_CHECK_PROG(BASH, bash, `which bash`, /bin/sh)
WARNING="This file is generated, do not edit"
AC_SUBST(WARNING)

# Can we rebuild the parsers?
# We really want flex and refuse other lex. So, the parser is portable and
# does not induce extra lib dependency
AC_PROG_FLEX(2.5.30)
		
# Can we rebuild the xml-lexers from the XML specification?
# if not, simply touch the flex source files (which are distributed in
#  tarballs even if generated by flexml) and hope for the best.
AC_CHECK_PROG(FLEXML,flexml,,NOTFOUND)
AM_CONDITIONAL(HAVE_FLEXML,test x$FLEXML != NOTFOUND)

###################
## Makes the output
##


#    examples/bandwidth/Makefile examples/bandwidth/test_sg
#    examples/pastry/Makefile    examples/pastry/test_sg

AC_CONFIG_FILES([
  Makefile
  include/Makefile
  src/Makefile
  src/amok/Makefile
  examples/Makefile 
    examples/msg/Makefile          examples/msg/run_msg_test
    examples/gras/Makefile 
      examples/gras/ping/Makefile  examples/gras/ping/test_sg  examples/gras/ping/test_rl
      examples/gras/timer/Makefile examples/gras/timer/test_sg examples/gras/timer/test_rl
      examples/gras/chrono/Makefile examples/gras/chrono/test_sg examples/gras/chrono/test_rl
  doc/Makefile
    doc/Doxyfile.main           doc/Doxyfile.API          doc/Doxyfile.Examples   
  tools/compile-remote-worker tools/Makefile
    tools/gras/Makefile
  testsuite/Makefile
  testsuite/run_tests    
  testsuite/gras/trp_tcp_usage
  testsuite/gras/trp_file_usage
],[
    for file in                                                 \
     testsuite/run_tests                                        \
     testsuite/gras/trp_tcp_usage testsuite/gras/trp_file_usage \
     tools/compile-remote-worker                                \
     examples/msg/run_msg_test                                  \
     examples/gras/ping/test_sg   examples/gras/ping/test_rl    \
     examples/gras/timer/test_sg  examples/gras/timer/test_rl   \
     examples/gras/chrono/test_sg  examples/gras/chrono/test_rl \
    ; do                                                        \
      test -e $file && chmod +x $file;                          \
    done
    chmod +x $srcdir/tools/gras-check-arch;
])

#    examples/gras/pastry/test_sg   
#    examples/gras/bandwidth/test_sg 



#    examples/gras/saturate/Makefile  examples/gras/saturate/test_sg
#    examples/gras/alnem/Makefile     examples/gras/alnem/test_sg



AC_OUTPUT

echo "

Configuration of package \`${PACKAGE}' on $gras_arch_name (=$gras_arch):

	Compiler:	${CC}

	CFlags:		${CFLAGS}
	LDFlags:	${LDFLAGS}
"

exit 0;
