AC_PREREQ(2.59)

#We need a recent ACI
ACI_PREREQ(2003.01.16)

AC_INIT([simgrid],[2.91],[arnaud.legrand@imag.fr])
#AC_INIT([GRAS],[0.7],[martin.quinson@ens-lyon.fr])
AC_CONFIG_SRCDIR([include/gras.h])
AC_CONFIG_HEADERS([src/gras_config.h])

AC_REVISION($Revision$)
AC_CANONICAL_TARGET
AC_LANG([C])

AM_INIT_AUTOMAKE(gnu)
# MACRO_DIR should tell aclocal to search for my macro. That's the autoconf
# maintainer plan, but automake does not implement this yet (as in 1.8)
AC_CONFIG_MACRO_DIR(acmacro) 
# It seems to be called ACLOCAL_INCLUDE...
# A M_ACLOCAL_INCLUDE(acmacro)




AC_PROG_LIBTOOL

# declare the modules (no optional module)

dnl
dnl Load anything under acmacro/*.m4
dnl
dnl test -n "$ACLOCAL_FLAGS" && ACLOCAL="$ACLOCAL $ACLOCAL_FLAGS"
ACLOCAL="$ACLOCAL -I acmacro"


AC_PROG_CC
AM_SANITY_CHECK
AC_PROG_MAKE_SET

# Check architecture signature begin
GRAS_ARCH
# Check architecture signature end
GRAS_CHECK_STRUCT_COMPACTION

# Checks for header files.
AC_HEADER_STDC
AC_HEADER_TIME
AC_CHECK_HEADERS([sys/socket.h winsock.h winsock2.h \
                  sys/stat.h \
		  ucontext.h \
		  sys/time.h \
		  errno.h unistd.h ])
AC_CHECK_FUNCS([gettimeofday \
                getdtablesize \
                sysconf])

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_SIZE_T

# Checks for library functions.
dnl A C_FUNC_MEMCMP
dnl A C_CHECK_FUNCS([memset strchr strerror usleep])

# Can we rebuild the parsers?
# We really want flex and refuse other lex. So, the parser is portable and
# does not induce extra lib dependency
AC_PROG_LEX
if test "$LEX" != flex; then
  AC_MSG_NOTICE([Will not rebuild the parsers. I want flex.])
  LEX="$SHELL $missing_dir/missing flex";
else
changequote(<<, >>)dnl because of the regexp [[:blank:]]  
  FLEX_VERSION=`flex --version`;
  FLEX_VER_MAJ=`echo $FLEX_VERSION | sed 's/[^0-9\.]*//g' | sed 's/\..*//g'`;
  FLEX_VER_MED=`echo $FLEX_VERSION | sed 's/[^0-9\.]*//g' | sed 's/[0-9]*\.\([0-9]*\)\.[0-9]*/\1/g'`;
  FLEX_VER_MIN=`echo $FLEX_VERSION | sed 's/[^0-9\.]*//g' | sed 's/.*\.//g'`;
changequote([, ])dnl back to normality, there is no regexp afterward
  if test $FLEX_VER_MAJ -lt 2 ; then
    AC_MSG_NOTICE([Will not rebuild the parsers. Your flex is too old.])
    LEX="$SHELL $missing_dir/missing flex";
  else
    if test $FLEX_VER_MAJ -eq 2 ; then
      if test $FLEX_VER_MED -lt 5 ; then
        AC_MSG_NOTICE([Will not rebuild the parsers. Your flex is too old.])
        LEX="$SHELL $missing_dir/missing flex";
      else
        if test $FLEX_VER_MED -eq 5 ; then
          if test $FLEX_VER_MIN -lt 31 ; then
             AC_MSG_NOTICE([Will not rebuild the parsers. Your flex is too old.])
             LEX="$SHELL $missing_dir/missing flex";
          fi;
        fi;
      fi;
    fi;
  fi;
fi
		
# Can we rebuild the xml-lexers from the XML specification?
# if not, simply touch the flex source files (which are distributed in
#  tarballs even if generated by flexml) and hope for the best.
AC_CHECK_PROG(FLEXML,flexml,,NOTFOUND)
AM_CONDITIONAL(HAVE_FLEXML,test x$FLEXML != NOTFOUND)

dnl ####[ Search libs ]#######################################################
dnl A CI_PACKAGE([S imGrid],[the SimGrid simulator],[SG_init],[-lsimgrid],[simgrid.h],,:)
dnl A M_CONDITIONAL(HAVE_SG,test x$HAVE_SimGrid = xyes)
dnl if test x$HAVE_SimGrid = xyes; then
dnl   SIMGRID_DEP="-lsimgrid"
dnl else
  SIMGRID_DEP=""
dnl fi
AC_SUBST([SIMGRID_DEP])


dnl A C_CHECK_LIB(pthread, pthread_mutex_lock, LIBS="$LIBS -lpthread")
GRAS_DEP=""
AC_CHECK_LIB(nsl, gethostbyname, [GRAS_DEP="$GRAS_DEP -lnsl"])
AC_CHECK_LIB(socket, connect,    [GRAS_DEP="$GRAS_DEP -lsocket"])

AC_MSG_CHECKING(for extra dependencies of libgras)
case $host_os in
  *mingw* ) GRAS_DEP=-lws2_32;;
esac
	   
if test "x$GRAS_DEP" = x; then 
   AC_MSG_RESULT(none)
else
   AC_MSG_RESULT($GRAS_DEP)
fi
AC_SUBST([GRAS_DEP])

dnl ####[ maint mode ]#######################################################
AM_MAINTAINER_MODE
if test x$USE_MAINTAINER_MODE = xyes 
then
#   enable_iso_c=yes             # Let's go funky
   GNOME_COMPILE_WARNINGS(yes)
fi

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

dnl ####[ check for some programms ]###########################################
AC_CHECK_PROG(BASH, bash, `which bash`, /bin/sh)
WARNING="This file is generated, do not edit"
AC_SUBST(WARNING)

dnl ####[ Makes the output ]###################################################
#    examples/bandwidth/Makefile examples/bandwidth/test_sg
#    examples/pastry/Makefile    examples/pastry/test_sg

AC_CONFIG_FILES([
  Makefile
  include/Makefile
  src/Makefile
  src/amok/Makefile
  examples/Makefile 
    examples/msg/Makefile 
    examples/gras/ping/Makefile      examples/gras/ping/test_sg     examples/gras/ping/test_rl
  doc/Makefile
    doc/Doxyfile.main           doc/Doxyfile.API          doc/Doxyfile.Examples   
  tools/compile-remote-worker
  testsuite/Makefile
  testsuite/run_tests    
  testsuite/gras/trp_tcp_usage
  testsuite/gras/trp_file_usage
],[
     test -e testsuite/run_tests          && chmod +x testsuite/run_tests;
     test -e testsuite/gras/trp_tcp_usage && chmod +x testsuite/gras/trp_tcp_usage;
     test -e testsuite/gras/trp_file_usage&& chmod +x testsuite/gras/trp_file_usage;
     test -e tools/compile-remote-worker  && chmod +x tools/compile-remote-worker;
     test -e examples/gras/ping/test_sg   && chmod +x examples/gras/ping/test_sg;
     test -e examples/gras/ping/test_rl   && chmod +x examples/gras/ping/test_rl;
#     test -e examples/gras/bandwidth/test_sg   && chmod +x examples/gras/bandwidth/test_sg;
     test -e examples/gras/pastry/test_sg && chmod +x examples/gras/pastry/test_sg;
     chmod +x $srcdir/tools/gras-check-arch;
])




#    examples/gras/saturate/Makefile  examples/gras/saturate/test_sg
#    examples/gras/alnem/Makefile     examples/gras/alnem/test_sg



AC_OUTPUT

echo "

Configuration of package \`${PACKAGE}' on $gras_arch_name (=$gras_arch):

	Compiler:	${CC}

	CFlags:		${CFLAGS}
	LDFlags:	${LDFLAGS}
"

exit 0;
